plugins {
	id 'java'
	id 'application'
	id 'jacoco'
}

group = 'com.synapticloop'
version = '1.0.0'

repositories {
	mavenCentral()
}

sourceSets {
	integrationTest {
		java {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir file('src/integration/java')
		}
		resources.srcDir file('src/integration/resources')
	}
}

configurations {
	integrationTestCompile.extendsFrom testCompile
	integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
	// solrj
	implementation 'org.apache.solr:solr-solrj:9.6.0'
	implementation 'org.apache.solr:solr-solrj-zookeeper:9.6.0'

	// HTTP server
	implementation 'org.eclipse.jetty.http2:http2-server:10.0.20'
	implementation 'org.eclipse.jetty:jetty-servlet:10.0.20'

	// logging
	implementation 'org.slf4j:slf4j-api:2.0.13'
	implementation 'org.apache.logging.log4j:log4j-core:2.23.1'
	implementation 'org.apache.logging.log4j:log4j-api:2.23.1'
	implementation 'org.apache.logging.log4j:log4j-slf4j2-impl:2.23.1'


	// additional utilities
	implementation 'commons-io:commons-io:2.16.1'
	implementation 'org.json:json:20240303'

	// testing
	testImplementation platform('org.junit:junit-bom:5.9.1')
	testImplementation 'org.junit.jupiter:junit-jupiter'
	testImplementation 'org.mockito:mockito-core:5.12.0'
	testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.17.1'

	// integration testing
	integrationTestImplementation platform('org.junit:junit-bom:5.9.1')
	integrationTestImplementation 'org.junit.jupiter:junit-jupiter'
	integrationTestImplementation 'org.mockito:mockito-core:5.12.0'
	integrationTestImplementation 'com.fasterxml.jackson.core:jackson-databind:2.17.1'
	integrationTestImplementation 'commons-io:commons-io:2.16.1'
	integrationTestImplementation 'org.json:json:20240303'
}

application {
	mainClass = 'com.synapticloop.panl.Main'
}

distributions {
	main {
		distributionBaseName = 'solr-panl-9'
	}
}


test {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

tasks.register('integrationTest', Test) {
	useJUnitPlatform()
	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath
	outputs.upToDateWhen { false }
}

jacocoTestReport {
	dependsOn test
}

